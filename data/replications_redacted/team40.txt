Team: 40
Software: Stata
Version: ORIGINAL


clear all
set more off
use "ZA2900.dta"

	recode v3 (1=36) (2/3=276) (4=826) (6=840) (8=348) (9=.) (10=372) (12=578) (13=752) (14=203) (15=705) (16=616) (17=.) (18=643) (19=554) (20=124) (21=608) (22/23=376) (24=392) (25=724) (26=428) (27=250) (28=.) (30=756), gen(v3a)
	label define cntrylbl 36 "Australia" 124 "Canada" 152 "Chile" 158 "Taiwan" 191 "Croatia" 203 "Czech" 208 "Denmark" 246 "Finland" 250 "France" 276 "Germany" 348 "Hungary" 372 "Ireland" 376 "Isreal" 392 "Japan" 410 "S Korea" 428 "Latvia" 528 "Netherlands" 554 "New Zealand" 578 "Norway" 608 "Philippines" 616 "Poland" 620 "Portugal" 643 "Russia" 705 "Slovenia" 724 "Spain" 752 "Sweden" 756 "Switzerland" 826 "Great Britain" 840 "United States" 
	label values v3a cntrylbl


		recode v36 (1=4) (2=3) (3=2) (4=1), gen(govjobs)
		recode govjobs (1/2=0) (3/4=1), gen(dgovjobs)
		recode v38 (1=4) (2=3) (3=2) (4=1), gen(govhcare)
		recode govhcare (1/2=0) (3/4=1), gen(dhcare)
		recode v39 (1=4) (2=3) (3=2) (4=1), gen(govretire)
		recode govretire (1/2=0) (3/4=1), gen(dgovretire)
		recode v41 (1=4) (2=3) (3=2) (4=1), gen(govunemp)
		recode govunemp (1/2=0) (3/4=1), gen(dgovunemp)
		recode v42 (1=4) (2=3) (3=2) (4=1), gen(govincdiff)
		recode govincdiff (1/2=0) (3/4=1), gen(dgovincdiff)
		recode v44 (1=4) (2=3) (3=2) (4=1), gen(govhousing)
		recode govhousing (1/2=0) (3/4=1), gen(dgovhous)


		rename v201 age
		gen agesq=age*age
		recode v200 (1=0) (2=1), gen(female)
		rename v205 edcat
		recode edcat (1/3=1) (4=2) (5=3) (6=4) (7=5), gen(degree)
		label define edlabels 1 "Primary/less" 2 "Some Secondary" 3 "Secondary" 4 "Some Higher Ed" 5 "University or higher"
		label values degree edlabels
		recode degree (1/2=1) (nonmiss=0), gen(lesshs)
		recode degree (3/4=1) (nonmiss=0), gen(hs)
		recode degree (5=1) (nonmiss=0), gen(univ)
		rename v208 isco
		rename v209 occ2
		rename v215 hourswrk
		recode v206 (2/10=0), gen(ftemp)
		recode v206 (2/4=1) (nonmiss=0), gen(ptemp)
		recode v206 (5=1) (nonmiss=0), gen(unemp)
		recode v206 (6/10=1) (nonmiss=0), gen(nolabor)
		gen selfemp=v213==1
		replace selfemp=. if v206==.
		gen pubemp=(v212==1 | v212==2)
		replace pubemp=. if v206==.
		gen pvtemp=(selfemp==0 & pubemp==0)
		replace pvtemp=. if v206==.
		rename v218 faminc
		gen inczscore=.
		levelsof v3a, local(cntries)
		foreach cntryval of local cntries {
			zscore faminc if v3a==`cntryval', listwise
			replace inczscore=z_faminc if v3a==`cntryval'
			drop z_faminc
		}


		gen year=1996
		gen yr2006=0
		rename v3a cntry

save "ZA2900_formerge.dta", replace
use "ZA4700.dta", clear


		recode V25 (1=4) (2=3) (3=2) (4=1), gen(govjobs)
		recode govjobs (1/2=0) (3/4=1), gen(dgovjobs)
		recode V27 (1=4) (2=3) (3=2) (4=1), gen(govhcare)
		recode govhcare (1/2=0) (3/4=1), gen(dhcare)
		recode V28 (1=4) (2=3) (3=2) (4=1), gen(govretire)
		recode govretire (1/2=0) (3/4=1), gen(dgovretire)
		recode V30 (1=4) (2=3) (3=2) (4=1), gen(govunemp)
		recode govunemp (1/2=0) (3/4=1), gen(dgovunemp)
		recode V31 (1=4) (2=3) (3=2) (4=1), gen(govincdiff)
		recode govincdiff (1/2=0) (3/4=1), gen(dgovincdiff)
		recode V33 (1=4) (2=3) (3=2) (4=1), gen(govhousing)
		recode govhousing (1/2=0) (3/4=1), gen(dgovhous)

		gen agesq=age*age
		recode sex (1=0) (2=1), gen(female)
		rename degree edcat
		recode edcat (0=1), gen(degree)
		label define edlabels 1 "Primary/less" 2 "Some Secondary" 3 "Secondary" 4 "Some Higher Ed" 5 "University or higher"
		label values degree edlabels
		recode degree (1/2=1) (nonmiss=0), gen(lesshs)
		recode degree (3/4=1) (nonmiss=0), gen(hs)
		recode degree (5=1) (nonmiss=0), gen(univ)
		rename wrkst empstat
		rename ISCO88 isco // see pg 137 in codebook
		rename wrkhrs hourswrk
		recode empstat (2/10=0), gen(ftemp)
		recode empstat (2/4=1) (nonmiss=0), gen(ptemp)
		recode empstat (5=1) (nonmiss=0), gen(unemp)
		recode empstat (6/10=1) (nonmiss=0), gen(nolabor)
		gen selfemp=wrktype==4
		replace selfemp=. if empstat==.
		gen pubemp=(wrktype==1 | wrktype==2)
		replace pubemp=. if empstat==.
		gen pvtemp=(selfemp==0 & pubemp==0)
		replace pvtemp=. if empstat==.
		gen inczscore=.
		local incvars = "AU_INC CA_INC CH_INC CL_INC CZ_INC DE_INC DK_INC DO_INC ES_INC FI_INC FR_INC GB_INC HR_INC HU_INC IE_INC IL_INC JP_INC KR_INC LV_INC NL_INC NO_INC NZ_INC PH_INC PL_INC PT_INC RU_INC SE_INC SI_INC TW_INC US_INC UY_INC VE_INC ZA_INC" 
		foreach incvar of local incvars {
			zscore `incvar', listwise
			replace inczscore=z_`incvar' if z_`incvar'!=.
			drop z_`incvar'
		}
	
		rename V3a cntry
		gen year=2006
		gen yr2006=1

save "ZA4700_formerge.dta", replace

append using "ZA2900_formerge.dta"
	sort cntry year
	merge m:1 cntry year using "BradyFinnigan2014CountryData.dta"
	recode cntry (36=1) (124=1) (208=1) (246=1) (250=1) (276=1) (372=1) (392=1) (528=1) (554=1) (578=1) (620=1) (724=1) (752=1) (756=1) (826=1) (840=1) (else=0), gen(orig17)
	recode cntry (36=1) (124=1) (250=1) (276=1) (372=1) (392=1) (554=1) (578=1) (724=1) (752=1) (756=1) (826=1) (840=1) (else=0), gen(orig13)

save "ZA29004700country.dta", replace



clear all
set more off

		global data "ZA29004700country.dta"
		global regtable "excel alpha(0.001, 0.01, 0.05) sym(***, **, *) ctitle(`depvar') eform bdec(3) sdec(2) stats(coef tstat) onecol append"
		global depvars "dgovjobs dgovunemp dgovincdiff dgovretire dgovhous dhcare"
		global controls "age agesq female lesshs univ ptemp unemp nolabor selfemp inczscore yr2006"
		global cntryvars "foreignpct netmigpct socx emprate"
		use $data, clear
		keep if orig13
		keep $depvars $cntryvars $controls cntry
		egen allcontrols = rowmiss($controls)
		recode allcontrols (0=1) (nonmiss=0)
		quietly tab cntry, gen(cntryfe)

	foreach depvar in $depvars {
		qui logit `depvar' foreignpct $controls cntryfe*
		outreg2 using "$desktop\results/forborn9606", excel alpha(0.001, 0.01, 0.05)  sym(***, **, *) eform bdec(3) sdec(2) stats(coef tstat) onecol append
	}
	foreach depvar in $depvars {
		qui logit `depvar' foreignpct socx $controls cntryfe*
		outreg2 using "$desktop\results/forborn9606socx", excel alpha(0.001, 0.01, 0.05)  sym(***, **, *) eform bdec(3) sdec(2) stats(coef tstat) onecol append
	}
	foreach depvar in $depvars {
		qui logit `depvar' foreignpct emprate $controls cntryfe*
		outreg2 using "$desktop\results/forborn9606emprate", excel alpha(0.001, 0.01, 0.05)  sym(***, **, *) eform bdec(3) sdec(2) stats(coef tstat) onecol append
	}

	foreach depvar in $depvars {
		qui logit `depvar' netmigpct $controls cntryfe*
		outreg2 using "$desktop\results/netmig9606", excel alpha(0.001, 0.01, 0.05)  sym(***, **, *) eform bdec(3) sdec(2) stats(coef tstat) onecol append
	}
	foreach depvar in $depvars {
		qui logit `depvar' netmigpct socx $controls cntryfe*
		outreg2 using "$desktop\results/netmig9606socx", excel alpha(0.001, 0.01, 0.05)  sym(***, **, *) eform bdec(3) sdec(2) stats(coef tstat) onecol append
	}
	foreach depvar in $depvars {
		qui logit `depvar' netmigpct emprate $controls cntryfe*
		outreg2 using "$desktop\results/netmig9606emprate", excel alpha(0.001, 0.01, 0.05)  sym(***, **, *) eform bdec(3) sdec(2) stats(coef tstat) onecol append
	}
	
	foreach depvar in $depvars {
		qui logit `depvar' netmigpct foreignpct $controls cntryfe*
		outreg2 using "$desktop\results/netmig9606forborn", excel alpha(0.001, 0.01, 0.05)  sym(***, **, *) eform bdec(3) sdec(2) stats(coef tstat) onecol append
	}
	foreach depvar in $depvars {
		qui logit `depvar' $controls cntryfe*
		outreg2 using "$desktop\results/controls9606", excel alpha(0.001, 0.01, 0.05)  sym(***, **, *) eform bdec(3) sdec(2) stats(coef tstat) onecol append
	}
































*** DESCRIPTIVES for tables S1 and S2***
preserve
	keep if allcontrols & year==1996 & dgovincdiff<.
	keep $depvars $cntryvars $controls
	outreg2 using "$desktop/results/desc1996", replace noaster excel sideway bdec(3) sdec(3) sum(detail) eqkeep(mean sd)
restore

preserve
	keep if allcontrols & year==1996
	collapse $depvars, by(cntry)
	outsheet cntry $depvars using "$desktop\results/depvars1996", comma replace
restore
