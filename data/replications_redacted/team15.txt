Team: 15
Software: Stata
Version: ORIGINAL

clear
use "ZA2900.dta"
numlabel _all, add

//Country selection

tab v3
recode v3 (1=36) (20=124) (27=250) (2/3=276) (10=372) (24=392) (19=554) (12=578) (25=724) (13=752) ///
(30=756) (4=826) (6=840) (8/9=.) (14/18=.) (21/23=.) (26=.) (28=.), gen(cntry)
label define cntry 36 "Australia" 124 "Canada" 250 "France" 276 "Germany" 372 "Ireland" ///
392 "Japan" 554 "New Zealand" 578 "Norway" 724 "Spain" 752 "Sweden" 756 "Switzerland" ///
826 "UK" 840 "USA"
label values cntry cntry

rename v36 jobs 
recode jobs (1/2=1) (3/4=0), gen(djobs)

rename v38 health
recode health (1/2=1) (3/4=0), gen(dhealth)

rename v39 retirement
recode retirement (1/2=1) (3/4=0), gen(dretirement)

rename v41 unemp
recode unemp (1/2=1) (3/4=0), gen(dunemp)

rename v42 income
recode income (1/2=1) (3/4=0), gen(dincome)

rename v44 housing
recode housing (1/2=1) (3/4=0), gen(dhousing)
tab dhousing housing, m

label define dav 1 "should be" 0 "should not be"
label values djobs-dhousing dav

rename v201 age
gen agesq=age^2

recode v200 (1=0) (2=1), gen(female)

rename v205 education
gen lesssec=.
replace lesssec=1 if education>=1 & education<=4
replace lesssec= 0 if education >=5 & education<=7
label variable lesssec "Less than secondary education"
gen uni_above=.
replace uni_above=1 if education==7
replace uni_above= 0 if education >=1 & education<=6
label variable uni_above "university education or above"
gen highsec=.
replace highsec=1 if education==5 | education==6
replace highsec=0 if education>=1 & education<=4 | education==7
label variable highsec "secondary education"

recode v206 (2/4=1) (nonmis=0), gen(pttime)
recode v206 (5=1)  (nonmis=0), gen(unempl)
recode v206 (6/10=1) (nonmis=0), gen(nolab)
recode v206 (1=1) (nonmis=0), gen(fltime)
  
label variable pttime "part-time employed"
label variable unempl "unemployed"
label variable nolab "not in labour force"
label variable fltime "full-time employed"

generate slfemp=0
replace slfemp=1 if v213==1
replace slfemp=. if v206==.
tab v213 slfemp, m

sort cntry
rename v218 hhinc

gen hhinc_z=.
levelsof cntry, local(countries)
foreach value of local countries {
	zscore hhinc if cntry==`value', listwise
	replace hhinc_z=z_hhinc if cntry==`value'
	drop z_hhinc
}

gen year=1996
drop if cntry==.
save "ISSP1996_recoded.dta", replace

clear
use "ZA4700.dta"
numlabel _all, add
recode V3 (36=36) (124=124) (250=250) (276.1/276.2=276) (372=372) (392=392) (554=554) (578=578) (724=724) (752=752) ///
(756=756) (826.1=826) (840=840) (else=.), gen(cntry)

label define cntry 36 "Australia" 124 "Canada" 250 "France" 276 "Germany" 372 "Ireland" ///
392 "Japan" 554 "New Zealand" 578 "Norway" 724 "Spain" 752 "Sweden" 756 "Switzerland" ///
826 "UK" 840 "USA"

label values cntry cntry

rename V25 jobs 
recode jobs (1/2=1) (3/4=0), gen(djobs)
rename V27 health
recode health (1/2=1) (3/4=0), gen(dhealth)
rename V28 retirement
recode retirement (1/2=1) (3/4=0), gen(dretirement)
rename V30 unemp
recode unemp (1/2=1) (3/4=0), gen(dunemp)
rename V31 income
recode income (1/2=1) (3/4=0), gen(dincome)
rename V33 housing
recode housing (1/2=1) (3/4=0), gen(dhousing)

label define dav 1 "should be" 0 "should not be"
label values djobs-dhousing dav

sort cntry

gen agesq=age^2

recode sex (1=0) (2=1), gen(female)

rename degree education

gen lesssec=.
replace lesssec=1 if education>=0 & education<=2
replace lesssec= 0 if education>=3 & education<=5
label variable lesssec "Less than higher secondary education"

gen uni_above=.
replace uni_above=1 if education==5
replace uni_above=0 if education >=0 & education<=4
label variable uni_above "university education or above"

gen highsec=.
replace highsec=1 if education==3 | education==4
replace highsec=0 if education>=0 & education<=2 | education==5
label variable highsec "higher secondary education"

recode wrkst (2/4=1) (nonmis=0), gen(pttime)
recode wrkst (5=1)  (nonmis=0), gen(unempl)
recode wrkst (6/10=1) (nonmis=0), gen(nolab)
recode wrkst (1=1) (nonmis=0), gen(fltime)
  
label variable pttime "part-time employed"
label variable unempl "unemployed"
label variable nolab "not in labour force"
label variable fltime "full-time employed"

generate slfemp=0
replace slfemp=1 if wrktype==4
replace slfemp=. if wrkst==.

gen hhinc_z=.
zscore *_INC

local hhinc="z_AU_INC z_CA_INC z_CH_INC z_CL_INC z_CZ_INC z_DE_INC z_DK_INC z_DO_INC z_ES_INC z_FI_INC z_FR_INC z_GB_INC z_HR_INC z_HU_INC z_IE_INC z_IL_INC z_JP_INC z_KR_INC z_LV_INC z_NL_INC z_NO_INC z_NZ_INC z_PH_INC z_PL_INC z_PT_INC z_RU_INC z_SE_INC z_SI_INC z_TW_INC z_US_INC z_UY_INC z_VE_INC z_ZA_INC" 
foreach value of local hhinc {
	replace hhinc_z=`value' if `value'!=.
	}
	drop z_AU_INC - z_ZA_INC	


sort cntry
gen year=2006
drop if cntry==.
save "ISSP2006_recoded.dta", replace

clear
use "ISSP2006_recoded.dta"

append using "ISSP1996_recoded.dta"
numlabel _all, add
sort year cntry
merge m:1 cntry year using "2014countrydata.dta"
drop if cntry == 191 | cntry == 203 | cntry == 208| cntry == 246 | cntry== 348 | cntry== 376 | cntry == 410 | cntry == 428 | cntry == 528 | cntry == 616 | cntry == 620 | cntry == 643 | cntry == 705

sort year
gen yr2006=0
replace yr2006=1 if year==2006
quietly tab cntry, gen(cntryfe)

save "meged9609_recoded.dta", replace

clear
use "meged9609_recoded.dta"

tab cntry

global dependent "djobs dunemp dincome dretirement dhousing dhealth"
global controls "age agesq female lesssec uni_above pttime unempl nolab slfemp hhinc_z yr2006"
global cntryvar "foreignpct netmigpct socx emprate" 

egen missing = rowmiss($controls) 

keep if missing == 0

foreach dv in $dependent {
	logit `dv' foreignpct $controls cntryfe*, or
	outreg2 using "output/table4.xls", excel alpha(0.001, 0.01, 0.05) sym(***, **, *) eform bdec(3) sdec(2) stat(coef tstat) onecol append addstat(Pseudo R-squared, `e(r2_p)')
}

foreach dv in $dependent {
	logit `dv' foreignpct socx $controls cntryfe*, or
	outreg2 using "output/table4.xls", excel alpha(0.001, 0.01, 0.05) sym(***, **, *) eform bdec(3) sdec(2) stat(coef tstat) onecol append addstat(Pseudo R-squared, `e(r2_p)')
}

foreach dv in $dependent {
	logit `dv' foreignpct emprate $controls cntryfe*, or
	outreg2 using "output/table4.xls", excel alpha(0.001, 0.01, 0.05) sym(***, **, *) eform bdec(3) sdec(2) stat(coef tstat) onecol append addstat(Pseudo R-squared, `e(r2_p)')
}


foreach dv in $dependent {
	logit `dv' netmigpct $controls cntryfe*, or
	outreg2 using "output/table5.xls", excel alpha(0.001, 0.01, 0.05) sym(***, **, *) eform bdec(3) sdec(2) stat(coef tstat) onecol append addstat(Pseudo R-squared, `e(r2_p)')
}

foreach dv in $dependent {
	logit `dv' netmigpct socx $controls cntryfe*, or
	outreg2 using "output/table5.xls", excel alpha(0.001, 0.01, 0.05) sym(***, **, *) eform bdec(3) sdec(2) stat(coef tstat) onecol append addstat(Pseudo R-squared, `e(r2_p)')
}

foreach dv in $dependent {
	logit `dv' netmigpct emprate $controls cntryfe*, or
	outreg2 using "output/table5.xls", excel alpha(0.001, 0.01, 0.05) sym(***, **, *) eform bdec(3) sdec(2) stat(coef tstat) onecol append addstat(Pseudo R-squared, `e(r2_p)')
}

foreach dv in $dependent {
	logit `dv' netmigpct foreignpct $controls cntryfe*, or
	outreg2 using "output/table5.xls", excel alpha(0.001, 0.01, 0.05) sym(***, **, *) eform bdec(3) sdec(2) stat(coef tstat) onecol append addstat(Pseudo R-squared, `e(r2_p)')
}









