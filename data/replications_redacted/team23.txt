Team: 23
Software: Stata
Version: Original

clear all
set more off
global vars country year oldcare unempl incdiff jobs stock delta_stock socexp emplrate female ///
			age age2 edu lowedu secedu hiedu lfstatus ptime noactive activeue fulltime 


use ZA2900.dta

g country = "" 
replace country = "Australia" if v3 == 1
replace country = "Canada" if v3 == 20
replace country = "France" if v3 == 27
replace country = "Germany" if v3 == 2 | v3 == 3
replace country = "Ireland" if v3 == 10
replace country = "Japan" if v3 == 24
replace country = "New Zealand" if v3 == 19
replace country = "Norway" if v3 == 12
replace country = "Spain" if v3 == 25
replace country = "Sweden" if v3 == 13
replace country = "Switzerland" if v3 == 30 
replace country = "United Kingdom" if v3 == 4 
replace country = "United States" if v3 == 6

drop if country == ""

g year = 1996

g oldcare = .
	replace oldcare = 0 if v39 == 3 | v39 == 4
	replace oldcare = 1 if v39 == 1 | v39 == 2

g unempl = .
	replace unempl = 0 if v41 == 3 | v41 == 4
	replace unempl = 1 if v41 == 1 | v41 == 2
	
g incdiff = .
	replace incdiff = 0 if v42 == 3 | v42 == 4
	replace incdiff = 1 if v42 == 1 | v42 == 2

g jobs = .
	replace jobs = 0 if v36 == 3 | v36 == 4
	replace jobs = 1 if v36 == 1 | v36 == 2

g female = .
	replace female = 0 if v200 == 1
	replace female = 1 if v200 == 2
g age = v201
g age2 = age^2
g lowedu = .
	replace lowedu = 0 if v205 != .
	replace lowedu = 1 if v205 <= 3
g secedu = .
	replace secedu = 0 if v205 != .
	replace secedu = 1 if v205 == 4 | v205 == 5
g hiedu = .
	replace hiedu = 0 if v205 != .
	replace hiedu = 1 if v205 >= 6 & v205 != .
g edu = .
	replace edu = 0 if lowedu == 1
	replace edu = 1 if secedu == 1
	replace edu = 2 if hiedu == 1
la def edu 0 "primary" 1 "secondary" 2 "tertiary"	
la val edu edu

g ptime = .
	replace ptime = 0 if v206 != .
	replace ptime = 1 if v206 == 2
g activeue = .
	replace activeue = 0 if v206 != .
	replace activeue = 1 if v206 == 5
g fulltime = .
	replace fulltime = 0 if v206 != .
	replace fulltime = 1 if v206 == 1
g noactive = .
	replace noactive = 0 if v206 != .
	replace noactive = 1 if v206 != 2 & v206 != 5 & v206 != 1 & v206 != .
	
g lfstatus = .
	replace lfstatus = 0 if fulltime == 1
	replace lfstatus = 1 if ptime == 1
	replace lfstatus = 2 if activeue == 1
	replace lfstatus = 3 if noactive == 1
la def lfstatus 0 "full-time" 1 "part-time" 2 "unemployed" 3 "not active"
la val lfstatus lfstatus	

	
save temp_ZA2900.dta, replace

use ZA4700.dta, clear

g country = ""
replace country = "Australia" if V3 == 36
replace country = "Canada" if V3 == 124
replace country = "France" if V3 == 250
replace country = "Germany" if V3a == 276
replace country = "Ireland" if V3 == 372
replace country = "Japan" if V3 == 392
replace country = "New Zealand" if V3 == 554
replace country = "Norway" if V3 == 578
replace country = "Spain" if V3 == 724
replace country = "Sweden" if V3 == 752
replace country = "Switzerland" if V3 == 756 
replace country = "United Kingdom" if V3a == 826 
replace country = "United States" if V3 == 840

drop if country == ""

g year = 2006

g oldcare = .
	replace oldcare = 0 if V28 == 3 | V28 == 4
	replace oldcare = 1 if V28 == 1 | V28 == 2

g unempl = .
	replace unempl = 0 if V30 == 3 | V30 == 4
	replace unempl = 1 if V30 == 1 | V30 == 2

g incdiff = .
	replace incdiff = 0 if V31 == 3 | V31 == 4
	replace incdiff = 1 if V31 == 1 | V31 == 2

g jobs = .
	replace jobs = 0 if V25 == 3 | V25 == 4
	replace jobs = 1 if V25 == 1 | V25 == 2

g female = .
	replace female = 0 if sex == 1
	replace female = 1 if sex == 2

g age2 = age^2 

g lowedu = .
	replace lowedu = 0 if degree != .
	replace lowedu = 1 if degree <= 1
g secedu = .
	replace secedu = 0 if degree != .
	replace secedu = 1 if degree == 2 | degree == 3
g hiedu = .
	replace hiedu = 0 if degree != .
	replace hiedu = 1 if degree == 4 | degree == 5
g edu = .
	replace edu = 0 if lowedu == 1
	replace edu = 1 if secedu == 1
	replace edu = 2 if hiedu == 1


g ptime = .
	replace ptime = 0 if wrkst != .
	replace ptime = 1 if wrkst == 2
g activeue = .
	replace activeue = 0 if wrkst != .
	replace activeue = 1 if wrkst == 5
g fulltime = .
	replace fulltime = 0 if wrkst != .
	replace fulltime = 1 if wrkst == 1
g noactive = .
	replace noactive = 0 if wrkst != .
	replace noactive = 1 if wrkst != 2 & wrkst != 5 & wrkst != 1 & wrkst != .
	
g lfstatus = .
	replace lfstatus = 0 if fulltime == 1
	replace lfstatus = 1 if ptime == 1
	replace lfstatus = 2 if activeue == 1
	replace lfstatus = 3 if noactive == 1
la def lfstatus 0 "full-time" 1 "part-time" 2 "unemployed" 3 "not active"
la val lfstatus lfstatus	

append using temp_ZA2900.dta

merge m:1 country year using orig_data/L2data.dta, nogen keep(1 3)

erase temp_ZA2900.dta

g stock = foreignpct
g delta_stock = netmigpct
g socexp = socx
g emplrate = emprate

keep $vars

encode country, g(ctry)

compress
save replication_workfile.dta, replace

clear all
set more off

global covars female age age2 ib(2).edu ib(1).lfstatus

global policy oldcare unempl incdiff jobs  


foreach i in $policy {
	logit `i' stock $covars i.year i.ctry, or
		est sto `i'_1
	logit `i' stock socexp $covars i.year i.ctry, or
		est sto `i'_2
	logit `i' stock emplrate $covars i.year i.ctry, or
		est sto `i'_3
	logit `i' delta_stock $covars i.year i.ctry, or
		est sto `i'_4
	logit `i' delta_stock socexp $covars i.year i.ctry, or
		est sto `i'_5
	logit `i' delta_stock emplrate $covars i.year i.ctry, or
		est sto `i'_6
}


forvalues i = 1/6 {
	if `i' == 1 {
		global newfile replace
		global obs noobs
		global keeping stock
	}
	else if `i' == 2 {
		global newfile append
		global obs noobs
		global keeping stock socexp
	}		
	else if `i' == 3 {
		global newfile append
		global obs noobs
		global keeping stock emplrate
	}		
	else if `i' == 4 {
		global newfile append
		global obs noobs
		global keeping delta_stock
	}		
	else if `i' == 5 {
		global newfile append
		global obs noobs
		global keeping delta_stock socexp
	}		
	else if `i' == 6 {
		global newfile append
		global obs
		global keeping delta_stock emplrate
	}			
esttab  oldcare_`i' unempl_`i' incdiff_`i' jobs_`i' ///
	using maintable.txt, $newfile ///
	eform  star(* 0.05 ** 0.01 *** 0.001) b(3) z(3)  keep($keeping _cons) ///
	nomtitles nonumbers plain
}

