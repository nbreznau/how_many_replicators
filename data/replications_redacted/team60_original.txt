TEAM: 60
SOFTWARE: STATA
VERSION: ORIGINAL

use "ZA2900.dta", clear

numlabel, add

capture drop job
gen job = .
replace job = 1 if (v36==1)|(v36==2)
replace job = 0 if (v36==3)|(v36==4)
label define yesno 1 "Yes" 0 "No"
label values job yesno
label variable job "Gvmt should provide jobs"

capture drop employ
gen employ = .
replace employ = 1 if (v41==1)|(v41==2)
replace employ = 0 if (v41==3)|(v41==4)
label values employ yesno
label variable employ "Gvmt should provide living for unemployed"

capture drop income
gen income = .
replace income = 1 if (v42==1)|(v42==2)
replace income = 0 if (v42==3)|(v42==4)
label values income yesno
label variable income "Gvmt should reduce income inequality"

capture drop old
gen old = .
replace old = 1 if (v39==1)|(v39==2)
replace old = 0 if (v39==3)|(v39==4)
tab old, mi
tab old v39
label values old yesno
label variable old "Gvmt should provide living for old"

capture drop house
gen house = .
replace house = 1 if (v44==1)|(v44==2)
replace house = 0 if (v44==3)|(v44==4)
label values house yesno
label variable house "Gvmt should provide housing"

capture drop sick
gen sick = .
replace sick = 1 if (v38==1)|(v38==2)
replace sick = 0 if (v38==3)|(v38==4)
label values sick yesno
label variable sick "Gvmt should provide healthcare"

capture drop age
gen age = v201
label variable age "Age in Years"

capture drop agesq
gen agesq = age*age
label variable agesq "Age Squared"
summ agesq

capture drop female
gen female = .
replace female = 1 if (v200==2)
replace female = 0 if (v200==1)
label values female yesno
label variable female "Female"

rename marital1 married
label variable married "Married"
label values married yesno

rename marital2 widowed
label variable widowed "Widowed"
label values widowed yesno

rename marital3 divorced
label variable divorced "Divorced"
label values divorced yesno

rename marital4 separated
label variable separated "Separated"
label values separated yesno

rename marital5 single
label variable single "Never Married"
label values single yesno

capture drop divsep
egen divsep = rmax(divorced separated)
tab divsep v202
label variable divsep "Divorced or Separated"
label values divsep yesno

capture drop hhsize
gen hhsize = v273
label variable hhsize "Household Size"
tab hhsize

capture drop childhh
gen childhh = 0 if (v274!=.)
replace childhh = 1 if (v274==2)|(v274==3)|(v274==4)| ///
						(v274==6)|(v274==7)|(v274==8)| ///
						(v274==10)|(v274==12)|(v274==14)| ///
						(v274==16)|(v274==18)|(v274==20)| ///
						(v274==22)|(v274==24)|(v274==26)
label values childhh yesno
label variable childhh "Children in the HHld"

capture drop urban
gen urban = 0 if (v275!=.)
replace urban = 1 if (v275==1)
label values urban yesno
label variable urban "Lives in Urban Area"

capture drop town
gen town = 0 if (v275!=.)
replace town = 1 if (v275==2)
label values town yesno
label variable town "Lives in Suburbs/Town"

capture drop rural
gen rural = 0 if (v275!=.)
replace rural = 1 if (v275==3)
label values rural yesno
label variable rural "Lives in Rural Area"

capture drop lowed
gen lowed = 0 if (v205!=.)
replace lowed = 1 if (v205>=1)&(v205<=4)
label values lowed yesno
label variable lowed "Education Less than High School"

capture drop meded
gen meded = 0 if (v205!=.)
replace meded = 1 if (v205>=5)&(v205<=6)
label values meded yesno
label variable meded "Education High School"

capture drop highed
gen highed = 0 if (v205!=.)
replace highed = 1 if (v205==7)
label values highed yesno
label variable highed "Education Degree of Above"

capture drop ft
gen ft = 0 if (v206!=.)
replace ft = 1 if (v206==1)
label values ft yesno
label variable ft "Full Time Employment"

capture drop pt
gen pt = 0 if (v206!=.)
replace pt = 1 if (v206==2)|(v206==3)|(v206==4)
label values pt yesno
label variable pt "Part Time Employment"

capture drop out
gen out = 0 if (v206!=.)
replace out = 1 if (v206>=6)&(v206<=10)
label values out yesno
label variable out "Out of the Labour Market"

capture drop un
gen un = 0 if (v206!=.)
replace un = 1 if (v206==5)
label values un yesno
label variable un "Unemployed"

capture drop ftself
gen ftself = 0 if (v206!=.)
replace ftself = 1 if (ft==1)&(v213==1)
label values ftself yesno
label variable ftself "Full Time Self Employed"

capture drop ftpub
gen ftpub = 0 if (v206!=.)
replace ftpub = 1 if (ft==1)&((v212==1)|(v212==2))
label values ftpub yesno
label variable ftpub "Full Time Self Public"

capture drop ftpriv
gen ftpriv = 0 if (v206!=.)
replace ftpriv = 1 if (ft==1)&((v212==3)|(v212==6))
label values ftpriv yesno
label variable ftpriv "Full Time Private"

capture drop incomescore
gen incomescore = .
label variable incomescore "Country Income Z Scores"

levelsof v3, local(countries)
foreach value of local countries {
	zscore v218 if v3 == `value', listwise
	replace incomescore = z_v218 if v3 ==`value'
	drop z_v218
}

capture drop religion
gen religion = .
replace religion = 1 if (v220==6)
replace religion = 2 if (v220>=4)&(v220<=5)
replace religion = 3 if (v220>=1)&(v220<=3)
label define religion 1 "Never" 2 "Low" 3 "High"
label values religion religion
label variable religion "Religious Attendance"
tab religion, gen(religion)
rename religion1 lowrelig
label variable lowrelig "Low Religious Attendance"
rename religion2 highrelig
label variable highrelig "High Religious Attendance"

capture drop country
gen country = .
replace country = 1 if (v3==1)
replace country = 2 if (v3==20)
replace country = 5 if (v3==27)
replace country = 6 if (v3==2)|(v3==3)
replace country = 7 if (v3==10)
replace country = 8 if (v3==24)
replace country = 10 if (v3==19)
replace country = 11 if (v3==12)
replace country = 13 if (v3==25)
replace country = 14 if (v3==13)
replace country = 15 if (v3==30)
replace country = 16 if (v3==4)
replace country = 17 if (v3==6)
label define country 1 "Australia" 2 "Canada"  3 "Denmark"  ///
					4 "Finland"  5 "France"  6 "Germany"  ///
					7 "Ireland"  8 "Japan"  9 "Netherlands"  ///
					10 "New Zealand"  11 "Norway"  12 "Portugal"  ///
					13 "Spain"  14 "Sweden"  15 "Switzerland"  ///
					16 "UK"  17 "USA" 
label values country country
numlabel country, add

keep lowrelig highrelig incomescore pt out un ftpub ftself ftpriv lowed meded highed ///
		urban rural town childhh hhsize divsep single widowed married ///
		female age agesq sick house old income employ job country v2
		
capture drop year
gen year = .
replace year = 1996
label variable year "Data Year"

sort country

save "1996ISSP.dta", replace

use "ZA4700.dta", clear

numlabel, add

capture drop job
gen job = .
replace job = 1 if (V25==1)|(V25==2)
replace job = 0 if (V25==3)|(V25==4)
label define yesno 1 "Yes" 0 "No"
label values job yesno
label variable job "Gvmt should provide jobs"

capture drop employ
gen employ = .
replace employ = 1 if (V30==1)|(V30==2)
replace employ = 0 if (V30==3)|(V30==4)
label values employ yesno
label variable employ "Gvmt should provide living for unemployed"

capture drop income
gen income = .
replace income = 1 if (V31==1)|(V31==2)
replace income = 0 if (V31==3)|(V31==4)
label values income yesno
label variable income "Gvmt should reduce income inequality"

capture drop old
gen old = .
replace old = 1 if (V28==1)|(V28==2)
replace old = 0 if (V28==3)|(V28==4)
label values old yesno
label variable old "Gvmt should provide living for old"

capture drop house
gen house = .
replace house = 1 if (V33==1)|(V33==2)
replace house = 0 if (V33==3)|(V33==4)
label values house yesno
label variable house "Gvmt should provide housing"

capture drop sick
gen sick = .
replace sick = 1 if (V27==1)|(V27==2)
replace sick = 0 if (V27==3)|(V27==4)
label values sick yesno
label variable sick "Gvmt should provide healthcare"

summ age
label variable age "Age in Years"

capture drop agesq
gen agesq = age*age
tab agesq
label variable agesq "Age Squared"
summ agesq

capture drop female
gen female = .
replace female = 1 if (sex==2)
replace female = 0 if (sex==1)
label values female yesno
label variable female "Female"

tab marital, gen(marital)

rename marital1 married
label variable married "Married"
label values married yesno

rename marital2 widowed
label variable widowed "Widowed"
label values widowed yesno

rename marital3 divorced
label variable divorced "Divorced"
label values divorced yesno

rename marital4 separated
label variable separated "Separated"
label values separated yesno

rename marital5 single
label variable single "Never Married"
label values single yesno

capture drop divsep
egen divsep = rmax(divorced separated)
tab divsep marital
label variable divsep "Divorced or Separated"
label values divsep yesno

capture drop hhsize
gen hhsize = hompop
label variable hhsize "Household Size"

capture drop childhh
gen childhh = 0 if (hhcycle!=.)
replace childhh = 1 if (hhcycle==2)|(hhcycle==3)|(hhcycle==4)| ///
						(hhcycle==6)|(hhcycle==7)|(hhcycle==8)| ///
						(hhcycle==10)|(hhcycle==12)|(hhcycle==14)| ///
						(hhcycle==16)|(hhcycle==18)|(hhcycle==20)| ///
						(hhcycle==22)|(hhcycle==24)|(hhcycle==26)| ///
						(hhcycle==28)|(hhcycle==29)
label values childhh yesno
label variable childhh "Children in the HHld"

capture drop urban
gen urban = 0 if (urbrural!=.)
replace urban = 1 if (urbrural==1)
label values urban yesno
label variable urban "Lives in Urban Area"

capture drop town
gen town = 0 if (urbrural!=.)
replace town = 1 if (urbrural==2)|(urbrural==3)
label values town yesno
label variable town "Lives in Suburbs/Town"

capture drop rural
gen rural = 0 if (urbrural!=.)
replace rural = 1 if (urbrural==4)|(urbrural==5)
label values rural yesno
label variable rural "Lives in Rural Area"

capture drop lowed
gen lowed = 0 if (degree!=.)
replace lowed = 1 if (degree>=0)&(degree<=2)
label values lowed yesno
label variable lowed "Education Less than High School"

capture drop meded
gen meded = 0 if (degree!=.)
replace meded = 1 if (degree>=3)&(degree<=4)
label values meded yesno
label variable meded "Education High School"

capture drop highed
gen highed = 0 if (degree!=.)
replace highed = 1 if (degree==5)
label values highed yesno
label variable highed "Education Degree of Above"

capture drop ft
gen ft = 0 if (wrkst!=.)
replace ft = 1 if (wrkst==1)
label values ft yesno
label variable ft "Full Time Employment"

capture drop pt
gen pt = 0 if (wrkst!=.)
replace pt = 1 if (wrkst==2)|(wrkst==3)|(wrkst==4)
label values pt yesno
label variable pt "Part Time Employment"

capture drop out
gen out = 0 if (wrkst!=.)
replace out = 1 if (wrkst>=6)&(wrkst<=10)
label values out yesno
label variable out "Out of the Labour Market"

capture drop un
gen un = 0 if (wrkst!=.)
replace un = 1 if (wrkst==5)
label values un yesno
label variable un "Unemployed"

capture drop ftself
gen ftself = 0 if (wrkst!=.)
replace ftself = 1 if (ft==1)&(wrktype==4)
label values ftself yesno
label variable ftself "Full Time Self Employed"

capture drop ftpub
gen ftpub = 0 if (wrkst!=.)
replace ftpub = 1 if (ft==1)&((wrktype==1)|(wrktype==2))
label values ftpub yesno
label variable ftpub "Full Time Self Public"

capture drop ftpriv
gen ftpriv = 0 if (wrkst!=.)
replace ftpriv = 1 if (ft==1)&((wrktype==3)|(wrktype==6))
label values ftpriv yesno
label variable ftpriv "Full Time Private"

capture drop incomescore
gen incomescore = .
label variable incomescore "Country Income Z Scores"
local incvars = "AU_INC CA_INC CH_INC CL_INC CZ_INC DE_INC DK_INC DO_INC ES_INC FI_INC FR_INC GB_INC HR_INC HU_INC IE_INC IL_INC JP_INC KR_INC LV_INC NL_INC NO_INC NZ_INC PH_INC PL_INC PT_INC RU_INC SE_INC SI_INC TW_INC US_INC UY_INC VE_INC ZA_INC" 
foreach incvar of local incvars {
	zscore `incvar', listwise
	replace incomescore=z_`incvar' if z_`incvar'!=.
	drop z_`incvar'
}

capture drop religion
gen religion = .
replace religion = 1 if (attend==8)
replace religion = 2 if (attend>=6)&(attend<=7)
replace religion = 3 if (attend>=1)&(attend<=5)
label define religion 1 "Never" 2 "Low" 3 "High"
label values religion religion
label variable religion "Religious Attendance"
tab religion, gen(religion)
rename religion1 lowrelig
label variable lowrelig "Low Religious Attendance"
rename religion2 highrelig
label variable highrelig "High Religious Attendance"

capture drop country
gen country = .
replace country = 1 if (V3==36)
replace country = 2 if (V3==124)
replace country = 3 if (V3==208)
replace country = 4 if (V3==246)
replace country = 5 if (V3==250)
replace country = 6 if (V3==376.1)|(V3==376.2)
replace country = 7 if (V3==372)
replace country = 8 if (V3==392)
replace country = 9 if (V3==528)
replace country = 10 if (V3==554)
replace country = 11 if (V3==578)
replace country = 12 if (V3==620)
replace country = 13 if (V3==724)
replace country = 14 if (V3==752)
replace country = 15 if (V3==756)
replace country = 16 if (V3==826.1)
replace country = 17 if (V3==840)

label define country 1 "Australia" 2 "Canada"  3 "Denmark"  ///
					4 "Finland"  5 "France"  6 "Germany"  ///
					7 "Ireland"  8 "Japan"  9 "Netherlands"  ///
					10 "New Zealand"  11 "Norway"  12 "Portugal"  ///
					13 "Spain"  14 "Sweden"  15 "Switzerland"  ///
					16 "UK"  17 "USA" 
label values country country

keep lowrelig highrelig incomescore pt out un ftpub ftself ftpriv lowed meded highed ///
		urban rural town childhh hhsize divsep single widowed married ///
		female age agesq sick house old income employ job country V2
		
capture drop year
gen year = .
replace year = 2006
label variable year "Data Year"
tab year, mi

save "2006ISSP.dta", replace

use "$path1\bradyfinnigan2014countrydata.dta", clear

numlabel, add

label variable foreignpct "Percent Foreign Born"
label variable netmigpct "Net Migration PCT"
label variable socx "Social Welfare Expenditure"
label variable socdem "Social Democratic Regime"
label variable liberal "Liberal Regime"
label variable emprate "Employment Rate"
label variable mcp "Multiculturalism Policy Index"

capture drop country
gen country = cntry
recode country (36=1) (124=2) (208=3) (246=4) (250=5) (276=6) ///
				(372=7) (392=8) (528=9) (554=10) (578=11) ///
				(620=12) (724=13) (752=14) (756=15) (826=16) ///
				(840=17)
label variable country "Country"
label define country 1 "Australia" 2 "Canada"  3 "Denmark"  ///
					4 "Finland"  5 "France"  6 "Germany"  ///
					7 "Ireland"  8 "Japan"  9 "Netherlands"  ///
					10 "New Zealand"  11 "Norway"  12 "Portugal"  ///
					13 "Spain"  14 "Sweden"  15 "Switzerland"  ///
					16 "UK"  17 "USA" 
label values country country
numlabel, add
drop if country>17

keep foreignpct netmigpct socx socdem liberal emprate mcp country year

save "countrydata.dta", replace

use "2006ISSP.dta", clear
append using "1996ISSP.dta"
merge m:1 country year using "countrydata.dta"
sort _merge
keep if (_merge==3)
drop _merge

tab year, gen(yrdummy)
rename yrdummy1 year96
label variable year96 "Year 1996"
rename yrdummy2 year06
label variable year06 "Year 2006"
tab1 year96 year06

tab country year
drop if (country==3)|(country==4)|(country==9)|(country==12)
tab country

capture drop countryfe
quietly tab country, gen(countryfe)
tab1 countryfe*

save "ISSP9606.dta", replace

use "ISSP9606.dta", clear

numlabel, add

bysort country: summ job employ income old house sick

global depvars "job employ income old house sick"
global controls "age agesq female lowed highed pt un out ftself ftpub incomescore lowrelig highrelig"
global cntryvars "foreignpct netmigpct socx emprate"

capture drop miss
egen miss = rowmiss($depvars $controls $cntryvars)
tab miss
capture drop complete
gen complete = .
replace complete = 1 if (miss == 0)
replace complete = 0 if (miss >= 1)&(miss <= 11)
label values complete yesno
label variable complete "Complete Case Sample"
tab complete

logit job 
esttab using "table4.csv", eform b(3) z(3)  wide replace

foreach depvar in $depvars {
	logit `depvar' foreignpct $controls countryfe* year06
	esttab using "table4.csv", eform b(3) z(3)  wide append 
	}
	
foreach depvar in $depvars {
	logit `depvar' foreignpct socx $controls countryfe* year06
	esttab using "table4.csv", eform b(3) z(3)  wide append 
	}
	
foreach depvar in $depvars {
	logit `depvar' foreignpct emprate $controls countryfe* year06
	esttab using "table4.csv", eform b(3) z(3) wide append 
	}





logit job, or
esttab using "table5.csv", eform b(3) z(3) wide replace


foreach depvar in $depvars {
	logit `depvar' netmigpct $controls countryfe* year06
	esttab using "table5.csv", eform b(3) z(3) wide append 
	}

foreach depvar in $depvars {
	logit `depvar' netmigpct socx $controls countryfe* year06
	esttab using "table5.csv", eform b(3) z(3) wide append 
	}
	
foreach depvar in $depvars {
	logit `depvar' netmigpct emprate $controls countryfe* year06
	esttab using "table5.csv", eform b(3) z(3) wide append 
	}
	
foreach depvar in $depvars {
	logit `depvar' netmigpct foreignpct $controls countryfe* year06
	esttab using "table5.csv", eform b(3) z(3) wide append  
	}


