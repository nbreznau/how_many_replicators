Team: 25
Software: Stata
Version: CURATED

use ZA2900.dta, clear

recode v3 (1=36) (2/3=276) (4=826) (6=840) (8=348) (9=.) (10=372) (12=578) (13=752) (14=203) (15=705) (16=616) (17=.) (18=643) (19=554) (20=124) (21=608) (22/23=376) (24=392) (25=724) (26=428) (27=250) (28=.) (30=756), gen(v3a)
label define cntrylbl 36 "Australia" 124 "Canada" 152 "Chile" 158 "Taiwan" 191 "Croatia" 203 "Czech" 208 "Denmark" 246 "Finland" 250 "France" 276 "Germany" 348 "Hungary" 372 "Ireland" 376 "Isreal" 392 "Japan" 410 "S Korea" 428 "Latvia" 528 "Netherlands" 554 "New Zealand" 578 "Norway" 608 "Philippines" 616 "Poland" 620 "Portugal" 643 "Russia" 705 "Slovenia" 724 "Spain" 752 "Sweden" 756 "Switzerland" 826 "Great Britain" 840 "United States" 
label values v3a cntrylbl

recode v36 (1=4) (2=3) (3=2) (4=1), gen(govjobs)
recode govjobs (1/2=0) (3/4=1), gen(dgovjobs)
lab var govj "rovide jobs for everyone"
lab var dgovj "rovide jobs for everyone"

recode v38 v39  v40  v41 v42-v45  (1=4) (2=3) (3=2) (4=1), ///
		gen(govhcare govretire indgrow govunemp govincdiff govstudents govhousing lawsenv)
recode govhcare govretire indgrow govunemp govincdiff govstudents govhousing lawsenv (1/2=0) (3/4=1), /// 
		gen(dhcare dgovretire dindgrow dgovunemp dgovincdiff dgovstud dgovhous dlawsenv)

rename v201 age
gen agesq=age*age
recode v200 (1=0) (2=1), gen(female)

rename v202 marst
recode marst (5=1) (nonmiss=0), gen(nevermar)
recode marst (2/5=0), gen(married)
recode marst (3/4=1) (nonmiss=0), gen(divorced)
recode marst (2=1) (nonmiss=0), gen(widow)

recode v203 (2=0), gen(partner)

rename v273 hhsize

recode v274 (2/4 10 12 14 16 18 20 22 24 26=1) (6/8=1) (nonmiss=0), gen(kidshh)

recode v275 (3=1) (nonmiss=0), gen(rural)
recode v275 (2=1) (nonmiss=0), gen(suburb)
rename v324 ETHNIC
rename v204 edyears
rename v205 edcat
recode edcat (1/3=1) (4=2) (5=3) (6=4) (7=5), gen(degree)
label define edlabels 1 "Primary/less" 2 "Some Secondary" 3 "Secondary" 4 "Some Higher Ed" 5 "University or higher"
label values degree edlabels

recode degree (1/2=1) (nonmiss=0), gen(lesshs)
recode degree (3/4=1) (nonmiss=0), gen(hs)
recode degree (5=1) (nonmiss=0), gen(univ)
rename v208 isco
rename v209 occ2
rename v215 hourswrk

recode v206 (2/10=0), gen(ftemp)
recode v206 (2/4=1) (nonmiss=0), gen(ptemp)
recode v206 (5=1) (nonmiss=0), gen(unemp)
recode v206 (6/10=1) (nonmiss=0), gen(nolabor)

gen selfemp=v213==1
replace selfemp=. if v206==.
gen pubemp=(v212==1 | v212==2)
replace pubemp=. if v206==.
gen pvtemp=(selfemp==0 & pubemp==0)
replace pvtemp=. if v206==.

rename v218 faminc

set more off
gen inczscore=.
levelsof v3, local(cntries)
foreach cntryval of local cntries {
	zscore faminc if v3==`cntryval', listwise
	replace inczscore=z_faminc if v3==`cntryval'
	drop z_faminc
}

recode v222 (2=0), gen(union)
rename v223 party
recode v220 (1/2=1) (nonmiss=0), gen(highrel)
recode v220 (3/5=1) (nonmiss=0), gen(lowrel)
recode v220 (6=1) (nonmiss=0), gen(norel)
rename v220 religion

gen year=1996
gen yr2006=0

rename v3a cntry
rename v325 wghts

save "ISSP96recode.dta", replace

use "ZA4700.dta", clear

recode V25 (1=4) (2=3) (3=2) (4=1), gen(govjobs)
recode govjobs (1/2=0) (3/4=1), gen(dgovjobs)
recode V27 (1=4) (2=3) (3=2) (4=1), gen(govhcare)
recode govhcare (1/2=0) (3/4=1), gen(dhcare)
recode V28 (1=4) (2=3) (3=2) (4=1), gen(govretire)
recode govretire (1/2=0) (3/4=1), gen(dgovretire)
recode V30 (1=4) (2=3) (3=2) (4=1), gen(govunemp)
recode govunemp (1/2=0) (3/4=1), gen(dgovunemp)
recode V31 (1=4) (2=3) (3=2) (4=1), gen(govincdiff)
recode govincdiff (1/2=0) (3/4=1), gen(dgovincdiff)
recode V33 (1=4) (2=3) (3=2) (4=1), gen(govhousing)
recode govhousing (1/2=0) (3/4=1), gen(dgovhous)

gen agesq=age*age

recode sex (1=0) (2=1), gen(female)
rename marital marst
recode marst (5=1) (nonmiss=0), gen(nevermar)
recode marst (2/5=0), gen(married)
recode marst (3/4=1) (nonmiss=0), gen(divorced)
recode marst (2=1) (nonmiss=0), gen(widow)

recode cohab (2=0), gen(partner)

rename hompop hhsize

recode hhcycle (2/4=1) (6/8=1) (nonmiss=0), gen(kidshh)
local i = 10
while `i' < 29 {
	replace kidshh=1 if hhcycle==`i'
	local i = `i' + 2
}

recode urbrural (1/3=0) (4/5=1), gen(rural)
recode urbrural (2/3=1) (nonmiss=0), gen(suburb)

rename educyrs edyears
rename degree edcat
recode edcat (0=1), gen(degree)
label define edlabels 1 "Primary/less" 2 "Some Secondary" 3 "Secondary" 4 "Some Higher Ed" 5 "University or higher"
label values degree edlabels

recode degree (1/2=1) (nonmiss=0), gen(lesshs)
recode degree (3/4=1) (nonmiss=0), gen(hs)
recode degree (5=1) (nonmiss=0), gen(univ)

rename wrkst empstat
rename ISCO88 isco // see pg 137 in codebook
rename wrkhrs hourswrk

recode empstat (2/10=0), gen(ftemp)
recode empstat (2/4=1) (nonmiss=0), gen(ptemp)
recode empstat (5=1) (nonmiss=0), gen(unemp)
recode empstat (6/10=1) (nonmiss=0), gen(nolabor)

gen selfemp=wrktype==4
replace selfemp=. if empstat==.
gen pubemp=(wrktype==1 | wrktype==2)
replace pubemp=. if empstat==.
gen pvtemp=(selfemp==0 & pubemp==0)
replace pvtemp=. if empstat==.

set more off
gen inczscore=.
local incvars = "AU_INC CA_INC CH_INC CL_INC CZ_INC DE_INC DK_INC DO_INC ES_INC FI_INC FR_INC GB_INC HR_INC HU_INC IE_INC IL_INC JP_INC KR_INC LV_INC NL_INC NO_INC NZ_INC PH_INC PL_INC PT_INC RU_INC SE_INC SI_INC TW_INC US_INC UY_INC VE_INC ZA_INC" 
foreach incvar of local incvars {
	zscore `incvar', listwise
	replace inczscore=z_`incvar' if z_`incvar'!=.
	drop z_`incvar'
}

rename union UNION
recode UNION (2/3=0), gen(union)
rename PARTY_LR party
recode attend (1/3=1) (nonmiss=0), gen(highrel)
recode attend (4/7=1) (nonmiss=0), gen(lowrel)
recode attend (8=1) (nonmiss=0), gen(norel)
rename attend religion

rename V3a cntry
rename weight wghts
gen year=2006
gen yr2006=1

gen mail=mode==34

save "ISSP06recode.dta", replace

append using "ISSP96recode.dta"
sort cntry year
merge m:1 cntry year using "BradyFinnigan2014CountryData.dta"
recode cntry (36=1) (124=1) (208=1) (246=1) (250=1) (276=1) (372=1) (392=1) (528=1) (554=1) (578=1) (620=1) (724=1) (752=1) (756=1) (826=1) (840=1) (else=0), gen(orig17)
recode cntry (36=1) (124=1) (250=1) (276=1) (372=1) (392=1) (554=1) (578=1) (724=1) (752=1) (756=1) (826=1) (840=1) (else=0), gen(orig13)
save "ISSP9606.dta", replace

use ISSP9606.dta, clear

ren (dgovjobs dgovunemp dgovincdiff dgovretire dgovhous dhcare) (Jobs	Unemp	IncomeDif	Old	House	Health)

global depvars "Jobs	Unemp	IncomeDif	Old	House	Health" 
global controls "age agesq female lesshs univ ptemp unemp nolabor selfemp inczscore yr2006"
global cntryvars "foreignpct netmigpct cforborn socx socdem liberal emprate assim diffex multi"

egen allcontrols = rowmiss($controls)
recode allcontrols (0=1) (nonmiss=0)
keep if allcontrols
keep if orig13

quietly tab cntry, gen(cntryfe)

eststo to411: logit Jobs foreignpct $controls cntryfe*
eststo to412: logit Unemp foreignpct $controls cntryfe*
eststo to413: logit IncomeDif foreignpct $controls cntryfe*
eststo to414: logit Old foreignpct $controls cntryfe*
eststo to415: logit House foreignpct $controls cntryfe*
eststo to416: logit Health foreignpct $controls cntryfe*

eststo to421: logit Jobs foreignpct socx $controls cntryfe*
eststo to422: logit Unemp foreignpct socx $controls cntryfe*
eststo to423: logit IncomeDif foreignpct socx $controls cntryfe*
eststo to424: logit Old foreignpct socx $controls cntryfe*
eststo to425: logit House foreignpct socx $controls cntryfe*
eststo to426: logit Health foreignpct socx $controls cntryfe*

eststo to431: logit Jobs foreignpct emprate $controls cntryfe*
eststo to432: logit Unemp foreignpct emprate $controls cntryfe*
eststo to433: logit IncomeDif foreignpct emprate $controls cntryfe*
eststo to434: logit Old foreignpct emprate $controls cntryfe*
eststo to435: logit House foreignpct emprate $controls cntryfe*
eststo to436: logit Health foreignpct emprate $controls cntryfe*

esttab to4* using Table4.csv, cells(b(fmt(a3) star)  z(fmt(2) par)) stats(N N_clust) eform nodep compress  const ///
			postfoot(`""' `"Exponentiated coefficients; z statistics in parentheses""' `"@starlegend""') csv noisily replace

eststo t511: logit Jobs netmigpct $controls  
eststo t512: logit Unemp netmigpct $controls  
eststo t513: logit IncomeDif netmigpct $controls  
eststo t514: logit Old netmigpct $controls  
eststo t515: logit House netmigpct $controls  
eststo t516: logit Health netmigpct $controls 

eststo t521: logit Jobs netmigpct socx $controls  
eststo t522: logit Unemp netmigpct socx $controls  
eststo t523: logit IncomeDif netmigpct socx $controls  
eststo t524: logit Old netmigpct socx $controls  
eststo t525: logit House netmigpct socx $controls  
eststo t526: logit Health netmigpct socx $controls 

eststo t531: logit Jobs netmigpct emprate $controls  
eststo t532: logit Unemp netmigpct emprate $controls  
eststo t533: logit IncomeDif netmigpct emprate $controls  
eststo t534: logit Old netmigpct emprate $controls  
eststo t535: logit House netmigpct emprate $controls  
eststo t536: logit Health netmigpct emprate $controls 

eststo t541: logit Jobs netmigpct foreignpct  $controls  
eststo t542: logit Unemp netmigpct foreignpct  $controls  
eststo t543: logit IncomeDif netmigpct foreignpct  $controls  
eststo t544: logit Old netmigpct foreignpct  $controls  
eststo t545: logit House netmigpct foreignpct  $controls  
eststo t546: logit Health netmigpct foreignpct  $controls 

esttab t5* using Table5.csv, cells(b(fmt(a3) star)  z(fmt(2) par)) stats(N N_clust) eform nodep compress  const ///
			postfoot(`""' `"Exponentiated coefficients; z statistics in parentheses""' `"@starlegend""') csv noisily replace


