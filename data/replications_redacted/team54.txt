Team: 54
Software: Stata
Version: ORIGINAL

version 15.1
clear
clear mata
clear matrix
set more off

use ZA2900.dta, clear

keep v2 v3 v39 v41 v42 v36 v200 v201 v205 v206
rename v2 respid

recode v39 (1/2=1) (3/4=0), gen(oldcare)
recode v41 (1/2=1) (3/4=0), gen(unemp)
recode v42 (1/2=1) (3/4=0), gen(income)
recode v36 (1/2=1) (3/4=0), gen(jobs)

drop v39 v41 v42 v36

recode v200 (1=0) (2=1), gen(sex)
drop v200

rename v201 age
gen age2=age^2

recode v205 (1/3=1) (4/6=2) (7=3), gen(edu)
drop v205

recode v206 (1=4) (2/3=1) (4=2) (6/10=2) (5=3), gen(employ)
drop v206

recode v3 (1=36) (2/3=276) (4=826) (6=840) (8=348) (9=.) (10=372) (12=578) (13=752) (14=203) (15=705) (16=616) (17=.) (18=643) (19=554) (20=124) ///
		(21=.) (22/23=376) (24=392) (25=724) (26=428) (27=250) (28=.) (30=756), gen(cntry) 
label define cntry    36  "Australia"   124 "Canada"  203 "Czech Republic"    250 "France"  276  "Germany"   348 "Hungary" 372 "Ireland" ///
  376 "Isreal"  392 "Japan" 428 "Latvia" 554 "New Zealand" 578 "Norway" 616 "Poland" 643 "Russia" 705  "Slovenia" 724 "Spain" ///
    756 "Switzerland" 826 "United Kingdom"  840  "United States"  752 "Sweden"
label val cntry cntry
drop v3

gen year=1996

save issp_1996.dta, replace

use ZA4700.dta,clear

keep V2 V3a V28 V30 V31 V25 sex age degree wrkst
rename V2 respid

recode V28 (1/2=1) (3/4=0), gen(oldcare)
recode V30 (1/2=1) (3/4=0), gen(unemp)
recode V31 (1/2=1) (3/4=0), gen(income)
recode V25 (1/2=1) (3/4=0), gen(jobs)

drop V28 V30 V31 V25

recode sex (1=0) (2=1)

gen age2=age^2

recode degree (0/1=1) (2/4=2) (5=3), gen(edu)
drop degree

recode wrkst (1=4) (2/3=1) (4=2) (6/10=2) (5=3), gen(employ)
drop wrkst

rename V3a cntry

gen year=2006

save issp_2006.dta, replace

append using issp_1996.dta

gen two_waves = .
levelsof cntry, local(cntries)
foreach c of local cntries {
 quietly fre year if cntry== `c'
  if r(r_valid) == 2 { 
  replace two_waves = 1 if cntry == `c'
   }
  else  {
  replace two_waves = 0 if cntry == `c' | cntry==.
  }
}



sort cntry year
merge m:1 cntry year using "L2data.dta"

drop if _merge==2 | _merge==1
drop if two_waves == 0
drop if socx==.
drop _merge two_waves

#delimit ;
order
respid
cntry
year
sex
age
age2
edu
employ
oldcare
unemp
income
jobs
emprate
foreignpct
socx
netmigpct
;
#delimit cr


lab var cntry "Country ISO-Code"
lab var sex "Female"
lab var age "Age"
lab var oldcare "Old Age Care"
lab var unemp "Unemployed"
lab var income "Reduce Income Differences"
lab var jobs "Jobs"
lab var age2 "Age-squared"
lab var edu "Education Categories"
lab var employ "Employment Categories"
lab var year "Year"
lab var emprate "Employment Rate"
lab var foreignpct "Immigrant Stock"
lab var socx "Social Welfare Expenditures"
lab var netmigpct "Change in Immigrant Stock"


lab def `:val l sex'				0 "Male" ///
						1 "Female", modify

lab def EDU					1 "Primary or less" ///
						2 "Secondary" ///
						3 "University or more", modify
lab val edu EDU

lab def EMPL					1 "Part-time" ///
						2 "Not active" ///
						3 "Active unemployed" ///
						4 "Full-time", modify
lab val employ EMPL

lab def POLATT					0 "Non-affirmative" ///
						1 "Affirmative", modify

lab val oldcare unemp income jobs POLATT

save repli_data_final.dta, replace


local indlev        "sex age age2 ib2.edu ib4.employ"

local fixeff     "i.cntry i.year" // dummies for countries and years

eststo m1: quietly logistic oldcare foreignpct			`indlev' `fixeff'
eststo m2: quietly logistic unemp foreignpct			`indlev' `fixeff'
eststo m3: quietly logistic income foreignpct			`indlev' `fixeff'
eststo m4: quietly logistic jobs foreignpct			`indlev' `fixeff'

eststo m5: quietly logistic oldcare foreignpct socx		`indlev' `fixeff'
eststo m6: quietly logistic unemp foreignpct socx		`indlev' `fixeff'
eststo m7: quietly logistic income foreignpct socx		`indlev' `fixeff'
eststo m8: quietly logistic jobs foreignpct socx		`indlev' `fixeff'

eststo m9: quietly logistic oldcare foreignpct emprate	`indlev' `fixeff'
eststo m10: quietly logistic unemp foreignpct emprate	`indlev' `fixeff'
eststo m11: quietly logistic income foreignpct emprate	`indlev' `fixeff'
eststo m12: quietly logistic jobs foreignpct emprate	`indlev' `fixeff'

eststo m13: quietly logistic oldcare netmigpct			`indlev' `fixeff'
eststo m14: quietly logistic unemp netmigpct			`indlev' `fixeff'
eststo m15: quietly logistic income netmigpct			`indlev' `fixeff'
eststo m16: quietly logistic jobs netmigpct				`indlev' `fixeff'


eststo m17: quietly logistic oldcare netmigpct socx		`indlev' `fixeff'
eststo m18: quietly logistic unemp netmigpct socx		`indlev' `fixeff'
eststo m19: quietly logistic income netmigpct socx		`indlev' `fixeff'
eststo m20: quietly logistic jobs netmigpct socx		`indlev' `fixeff'


eststo m21: quietly logistic oldcare netmigpct emprate	`indlev' `fixeff'
eststo m22: quietly logistic unemp netmigpct emprate	`indlev' `fixeff'
eststo m23: quietly logistic income netmigpct emprate	`indlev' `fixeff'
eststo m24: quietly logistic jobs netmigpct emprate		`indlev' `fixeff'

esttab m* using tables.txt, cells(b(star) z) cons eform fixed replace
esttab m* using tables.csv, cells(b(star) z) cons eform replace
